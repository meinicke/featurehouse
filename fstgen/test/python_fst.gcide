// -*- java -*-
// Copyright  Corporation for National Research Initiatives

options {
	STATIC = false;                          // multiple parsers
	COMMON_TOKEN_ACTION = true;              // CommonTokenAction(Token)
	USER_CHAR_STREAM = true;
	UNICODE_INPUT = true;
}


PARSER_BEGIN(PythonParser)

package de.ovgu.cide.fstgen.parsers.generated_python;
import cide.gparser.*;
import de.ovgu.cide.fstgen.ast.*;

public class PythonParser extends AbstractFSTParser {
	public PythonParser() {}
}

PARSER_END(PythonParser)

TOKEN_MGR_DECLS: {
	int indentation[] = {0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0};
	int level = 0;
	int dedents = 0;
	int parens = 0;
	int indent;

	boolean expect_indent = false;

	boolean compound = false;

	public boolean single_input = false;

	// parsing of partial sentence (interactive) mode
	public boolean partial = false;
	// control whether empty new lines on EOF force sentence closing NEWLINE even if indent
	// is expected,i.e. classic behavior at jython prompt (different from codeop behavior)
	public boolean stdprompt = false;

	public boolean generator_allowed = true;

	private static boolean DEBUG = false;

	static Token addDedent(Token previous) {
		Token t = new Token();
		t.kind = DEDENT;
		t.beginLine = previous.beginLine;
		t.endLine = previous.endLine;
		t.beginColumn = previous.beginColumn;
		t.endColumn = previous.endColumn;
		t.offset=previous.offset;
		t.length=0;
		t.image = "";
		t.specialToken = null;
		t.next = null;
		previous.next = t;
		return t;
	}

	void CommonTokenAction(Token t) {
		/*
			if not partial: EOF is expanded to token sequences comprising
				if single_input: [NEWLINE] necessary DEDENT NEWLINE (afterward EOF)
				otherwise      : [NEWLINE] necessary DEDENT EOF
			if partial: EOF expansion happens only if EOF preceded by empty line (etc),
			i.e. lexer is in MAYBE_FORCE_NEWLINE_IF_EOF state
		*/
		if (t.kind == EOF) {
		// System.out.println("EOF: "+single_input+", "+curLexState+", "+level);
			if (!partial || curLexState == MAYBE_FORCE_NEWLINE_IF_EOF) {
				if (curLexState == DEFAULT) {
					t.kind = NEWLINE;
				} else {
					t.kind = DEDENT;
					t.length=0;
					if (level >= 0) level -= 1;
				}
				while (level >= 0) {
					level--;
					t = addDedent(t);
				}
				if (!single_input) {
					t.kind = EOF;
					t.image = "<EOF>";
				} else {
					t.kind = NEWLINE;
					t.image = "<FORCENL>";
					single_input = false;
				}
			}
		} else if (t.kind == YIELD) {
			if (!generator_allowed) {
				t.kind = NAME;
			}
		}
	}

	void indenting(int ind) {
		indent = ind;
		if (indent == indentation[level])
			SwitchTo(INDENTATION_UNCHANGED);
		else
			SwitchTo(INDENTING);
	}
}


SPECIAL_TOKEN : {
	<SPACE: " ">
	| "\014"
	|  <CONTINUATION: ("\\") ("\r\n"|"\n"|"\r")>
}

SKIP: {
	"\t" |
	<NEWLINE1: ("\r\n"|"\n"|"\r")> {
		if (parens == 0) {
			indent = 0;
			input_stream.backup(1);
			if (level == 0)
				SwitchTo(FORCE_NEWLINE1);
			else
				SwitchTo(FORCE_NEWLINE2);
		}
	}
}

<FORCE_NEWLINE1> TOKEN : {
	<NEWLINE: ("\n" | "\r")> : INDENTATION_UNCHANGED
}

<FORCE_NEWLINE2> TOKEN : {
	<NEWLINE2: ("\n" | "\r")> { matchedToken.kind = NEWLINE; } : INDENTING
}

// causes expected warning
<MAYBE_FORCE_NEWLINE_IF_EOF> SKIP : {
	<""> { indenting(0); }
}

<INDENTING, INDENTATION_UNCHANGED> SPECIAL_TOKEN : {
	"\t" { indenting((indent/8+1)*8); }
	|  " " { indenting(indent+1); }
	|  "\014" { indenting(0); }
	|  <CRLF1: ("\r\n" | "\n" | "\r")> {
		//System.out.println("empty line");
		// if partial single_input (interactive) mode,
		// empty line (indent==0), and no parens open
		// or indentetion expected (if stdprompt == true, ovveride last cond)
		// consider forcing sentence closing NEWLINE if EOF
		if (partial && single_input && indent == 0 &&
				parens == 0  && (stdprompt || !expect_indent)) {
			//System.out.println("force newline");
			//backup a character!
			// - input_stream.backup(1); -
			SwitchTo(MAYBE_FORCE_NEWLINE_IF_EOF);
		} else
			indenting(0);
	}
}

<INDENTATION_UNCHANGED> SKIP : {
	<""> : DEFAULT
}

<INDENTING> TOKEN : {
	<DEDENT: ""> {
		if (indent > indentation[level]) {
			level++;
			indentation[level] = indent;
			matchedToken.kind=INDENT;
			matchedToken.image = "";
			//for (int i=0;i<indent;i++) matchedToken.image+=" ";
			matchedToken.length=0;
		} else if (level > 0) {
			Token t = matchedToken;
			level -= 1;
			while (level > 0 && indent < indentation[level]) {
				level--;
				t = addDedent(t);
			}
			if (indent != indentation[level]) {
				throw new TokenMgrError("inconsistent dedent",0);//TODO
			}
			t.next = null;
		}
	} : DEFAULT
}

<UNREACHABLE> TOKEN : {
	< INDENT:"<INDENT>">
//|     < DEDENT:"<DEDENT>">
}


<DEFAULT> SKIP: /* COMMENTS */ {
	<TRAILING_COMMENT: "#" (~["\n","\r"])*>
}

<INDENTING, INDENTATION_UNCHANGED> SKIP: /* COMMENTS */ {
	<SINGLE_LINE_COMMENT: "#" (~["\n","\r"])* ("\r\n" | "\n" | "\r")> {
		indenting(0);
	}
}


TOKEN : /* SEPARATORS */ {
	< LPAREN: "(" > {parens++;}
	|  < RPAREN: ")" > {parens--;}
	|  < LBRACE: "{" > {parens++;}
	|  < RBRACE: "}" > {parens--;}
	|  < LBRACKET: "[" > {parens++;}
	|  < RBRACKET: "]" > {parens--;}
	|  < SEMICOLON: ";" >
	|  < COMMA: "," >
	|  < DOT: "." >
	|  < COLON: ":" >
}


TOKEN : /* OPERATORS */ {
	< PLUS: "+" >
	|  < MINUS: "-" >
	|  < MULTIPLY: "*" >
	|  < DIVIDE: "/" >
	|  < FLOORDIVIDE: "//" >
	|  < POWER: "**" >
	|  < LSHIFT: "<<" >
	|  < RSHIFT: ">>" >
	|  < MODULO: "%" >
	|  < NOT: "~" >
	|  < XOR: "^" >
	|  < OR: "|" >
	|  < AND: "&" >
	|  < EQUAL: "=" >
	|  < GREATER: ">" >
	|  < LESS: "<" >
	|  < EQEQUAL: "==" >
	|  < EQLESS: "<=" >
	|  < EQGREATER: ">=" >
	|  < LESSGREATER: "<>" >
	|  < NOTEQUAL: "!=" >
	|  < PLUSEQ: "+=" >
	|  < MINUSEQ: "-=" >
	|  < MULTIPLYEQ: "*=" >
	|  < DIVIDEEQ: "/=" >
	|  < FLOORDIVIDEEQ: "//=" >
	|  < MODULOEQ: "%=" >
	|  < ANDEQ: "&=" >
	|  < OREQ: "|=" >
	|  < XOREQ: "^=" >
	|  < LSHIFTEQ: "<<=" >
	|  < RSHIFTEQ: ">>=" >
	|  < POWEREQ: "**=" >
}

TOKEN : /* KEYWORDS */ {
	< OR_BOOL: "or" >
	|  < AND_BOOL: "and" >
	|  < NOT_BOOL: "not" >
	|  < IS: "is" >
	|  < IN: "in" >
	|  < LAMBDA: "lambda" >
	|  < IF: "if" >
	|  < ELSE: "else" >
	|  < ELIF: "elif" >
	|  < WHILE: "while" >
	|  < FOR: "for" >
	|  < TRY: "try" >
	|  < EXCEPT: "except" >
	|  < DEF: "def" >
	|  < CLASS: "class" >
	|  < FINALLY: "finally" >
	|  < PRINT: "print" >
	|  < PASS: "pass" >
	|  < BREAK: "break" >
	|  < CONTINUE: "continue" >
	|  < RETURN: "return" >
	|  < YIELD: "yield" >
	|  < IMPORT: "import" >
	|  < FROM: "from" >
	|  < DEL: "del" >
	|  < RAISE: "raise" >
	|  < GLOBAL: "global" >
	|  < EXEC: "exec" >
	|  < ASSERT: "assert" >
	|  < AS: "as" >
	|  < WITH: "with" >
}


TOKEN : /* Python identifiers */ {
	< NAME: <LETTER> (<LETTER> | <DIGIT>)* >
	|  < #LETTER: ["_","a"-"z","A"-"Z"] >
}

TOKEN : /* Numeric literals */ {
	< DECNUMBER: ["1"-"9"] (["0"-"9"])* (["l", "L"])? | "0" >
	|  < HEXNUMBER: "0" ["x","X"] (["0"-"9","a"-"f","A"-"F"])+ (["l","L"])? >
	|  < OCTNUMBER: "0" (["0"-"7"])* (["l","L"])? >
	|  < FLOAT: (["0"-"9"])+ "." (["0"-"9"])* (<EXPONENT>)?
		| "." (["0"-"9"])+ (<EXPONENT>)?
		| (["0"-"9"])+ <EXPONENT> >
	|  < COMPLEX: (<DECNUMBER> | <FLOAT> | "0" <DECNUMBER> ) ["j", "J"] >
	|  < #EXPONENT: ["e","E"] (["+","-"])? (["0"-"9"])+ >
	|  < #DIGIT: ["0" - "9"] >
}

/*TOKEN : { <STRING: (["r", "R"])? ["'", "\"", "'''", "\"\"\""] >
    {
        int nquotes = matchedToken.image.length() > 2 ? 3 : 1;
        char quote = matchedToken.image.charAt(matchedToken.image.length()-1);

    }
}*/


MORE : /* Strings */ {
	< (["u", "U"]) (["r", "R"])? "'" > :  IN_USTRING11
	|  < (["u", "U"]) (["r", "R"])? "\"" > :  IN_USTRING21
	|  < (["u", "U"]) (["r", "R"])? "'''" > :  IN_USTRING13
	|  < (["u", "U"]) (["r", "R"])? "\"\"\"" > :  IN_USTRING23
	|  < (["r", "R"])? "'" > :  IN_STRING11
	|  < (["r", "R"])? "\"" > :  IN_STRING21
	|  < (["r", "R"])? "'''" > :  IN_STRING13
	|  < (["r", "R"])? "\"\"\"" > :  IN_STRING23
}

<IN_STRING11> TOKEN : { <SINGLE_STRING: "'"> {
    matchedToken.image = image.toString(); } : DEFAULT}
<IN_STRING21> TOKEN : { <SINGLE_STRING2: "\""> {
    matchedToken.image = image.toString(); } : DEFAULT}
<IN_STRING13> TOKEN : { <TRIPLE_STRING: "'''"> {
    matchedToken.image = image.toString(); } : DEFAULT}
<IN_STRING23> TOKEN : { <TRIPLE_STRING2: "\"\"\""> {
    matchedToken.image = image.toString(); } : DEFAULT}

<IN_USTRING11> TOKEN : { <SINGLE_USTRING: "'"> {
    matchedToken.image = image.toString(); } : DEFAULT}
<IN_USTRING21> TOKEN : { <SINGLE_USTRING2: "\""> {
    matchedToken.image = image.toString(); } : DEFAULT}
<IN_USTRING13> TOKEN : { <TRIPLE_USTRING: "'''"> {
    matchedToken.image = image.toString(); } : DEFAULT}
<IN_USTRING23> TOKEN : { <TRIPLE_USTRING2: "\"\"\""> {
    matchedToken.image = image.toString(); } : DEFAULT}

<IN_STRING11> MORE: {
	<"\\\r\n">           { image.setLength(image.length()-3); } : IN_STRING1NLC
	|  <("\\" ("\n"|"\r"))> { image.setLength(image.length()-2); } : IN_STRING1NLC
}

<IN_STRING21> MORE: {
	<"\\\r\n">           { image.setLength(image.length()-3); } : IN_STRING2NLC
	|  <("\\" ("\n"|"\r"))> { image.setLength(image.length()-2); } : IN_STRING2NLC
}

<IN_USTRING11> MORE: {
	<"\\\r\n">           { image.setLength(image.length()-3); } : IN_USTRING1NLC
	|  <("\\" ("\n"|"\r"))> { image.setLength(image.length()-2); } : IN_USTRING1NLC
}

<IN_USTRING21> MORE: {
	<"\\\r\n">           { image.setLength(image.length()-3); } : IN_USTRING2NLC
	|  <("\\" ("\n"|"\r"))> { image.setLength(image.length()-2); } : IN_USTRING2NLC
}

<IN_STRING1NLC> MORE: {
	<""> : IN_STRING11
}

<IN_STRING2NLC> MORE: {
	<""> : IN_STRING21
}

<IN_USTRING1NLC> MORE: {
	<""> : IN_USTRING11
}

<IN_USTRING2NLC> MORE: {
	<""> : IN_USTRING21
}

<IN_STRING11, IN_USTRING11> MORE: {
	<("\\" ("\\"|"'")) | ~["\n","\r"]>
}

<IN_STRING21, IN_USTRING21> MORE: {
	<("\\" ("\\"|"\"")) | ~["\n","\r"]>
}

<IN_STRING13, IN_STRING23, IN_USTRING13, IN_USTRING23> MORE: {
	<"\r\n"> {
		int l = image.length();
		image.setLength(l-1);
		image.setCharAt(l-2, '\n');
	}
	|  <"\n">
	|  <"\r"> { image.setCharAt(image.length()-1, '\n'); }
	|  <~["\n","\r"]>
	|  <"\\" ~["\n","\r"]>
}

GRAMMARSTART

@FSTNonTerminal
file_input
	:  (<NEWLINE>)? (stmt @!)* <EOF>
	;

@FSTExportName("{varargslist}")
parameters
	:  "(" [varargslist] ")"
	;

@FSTExportName("{TOSTRING}")
varargslist
	:  defaultarg (LL(2) "," defaultarg)* [LL(3) "," ExtraArgList] [LL(2) "," ExtraKeywordList] ["," <NONE>]
	|  LL(2) ExtraArgList ["," ExtraKeywordList]
	|  ExtraKeywordList
	;

ExtraArgList
	:  "*" Name
	;

ExtraKeywordList
	:  power Name
	;

defaultarg
	:  fpdef ["=" test]
	;

fpdef
	:  Name
	|  "(" fplist ")"
	;

fplist
	:  fpdef (LL(2) "," fpdef)* [","<NONE>]
	;

stmt
	:  @FSTNonTerminal(name="{simple_stmt}") simple_stmt
	|  @FSTTerminal(name="{AUTO}", merge="SemanticConflict") if_stmt
	|  @FSTTerminal(name="{AUTO}", merge="SemanticConflict") while_stmt
	|  @FSTTerminal(name="{AUTO}", merge="SemanticConflict") for_stmt
	|  @FSTTerminal(name="{AUTO}", merge="SemanticConflict") try_stmt
	|  @FSTTerminal(name="{AUTO}", merge="SemanticConflict") with_stmt
	|  @FSTTerminal(name="{funcdef}", merge="LineBased") @+ LOOK_AHEAD(" (decorators())? \"def\"") funcdef :: FunctionDefinition
	|  @FSTNonTerminal(name="{classdef}") classdef :: ClassDefinition
	;

@FSTNonTerminal(name="{small_stmt}")
simple_stmt
	:  &LI small_stmt (LL(2) ";" &LI small_stmt)* [";" <NONE>] <NEWLINE>
	;

small_stmt
	:  @FSTTerminal(name="{assignment_stmt}",merge="SemanticConflict") LOOK_AHEAD("Name() \"=\"") assignment_stmt
	|  @FSTTerminal(name="{expr_stmt}",merge="SemanticConflict") expr_stmt
	|  @FSTTerminal(name="{TOSTRING}",merge="SemanticConflict") print_stmt
	|  @FSTTerminal(name="{TOSTRING}",merge="SemanticConflict") del_stmt
	|  @FSTTerminal(name="{TOSTRING}",merge="SemanticConflict") pass_stmt
	|  @FSTTerminal(name="{TOSTRING}",merge="SemanticConflict") flow_stmt
	|  @FSTTerminal(name="{TOSTRING}",merge="SemanticConflict") import_stmt
	|  @FSTTerminal(name="{TOSTRING}",merge="SemanticConflict") global_stmt
	|  @FSTTerminal(name="{TOSTRING}",merge="SemanticConflict") exec_stmt
	|  @FSTTerminal(name="{TOSTRING}",merge="SemanticConflict") assert_stmt
	;

@FSTExportName("{<NAME>}")
assignment_stmt
	:  <NAME> "=" SmartTestList
	;

@FSTExportName("{SmartTestList}")
expr_stmt
	:  SmartTestList expr_stmtEnd
	;

expr_stmtEnd
	:  <PLUSEQ> SmartTestList
	|  <MINUSEQ> SmartTestList
	|  <MULTIPLYEQ> SmartTestList
	|  <DIVIDEEQ> SmartTestList
	|  <FLOORDIVIDEEQ> SmartTestList
	|  <MODULOEQ> SmartTestList
	|  <ANDEQ> SmartTestList
	|  <OREQ> SmartTestList
	|  <XOREQ> SmartTestList
	|  <LSHIFTEQ> SmartTestList
	|  <RSHIFTEQ> SmartTestList
	|  <POWEREQ> SmartTestList
	|  ("=" SmartTestList)*
	;

print_stmt
	:  LL(2) <PRINT> <RSHIFT> test [print_stmtEndP]
	|  LL(2) <PRINT> print_stmtEndA
	|  <PRINT>
	;

print_stmtEndP
	:  (LL(2) "," test)+ ["," <NONE>]
	;

print_stmtEndA
	:  test (LL(2) "," test)* ["," <NONE>]
	;

del_stmt
	:  <DEL> exprlist
	;

pass_stmt
	:  <PASS>
	;

flow_stmt
	:  <BREAK>
	|  <CONTINUE>
	|  return_stmt
	|  yield_stmt
	|  raise_stmt
	;

return_stmt
	:  <RETURN> [SmartTestList]
	;

yield_stmt
	:  <YIELD> SmartTestList
	;

raise_stmt
	:  <RAISE> [raise_stmt_end]
	;

raise_stmt_end
	:  test ["," testcommatest]
	;

testcommatest
	:  test ["," test]
	;

import_stmt
	:  <IMPORT> Import
	|  <FROM> ImportFrom
	;

Import
	:  dotted_as_name ("," dotted_as_name)*
	;

ImportFrom
	:  dotted_name <IMPORT> ImportFromEnd
	|  ("."<NONE>)+ ImportFrom_End
	;

ImportFrom_End
	:  [dotted_name] <IMPORT> ImportFromEnd
	;

ImportFromEnd
	:  "*"
	|  ["("<NONE>] import_as_name ("," import_as_name)* [")"<NONE>]
	;

dotted_as_name
	:  dotted_name ["as" Name]
	;

dotted_name
	:  AnyName ("." AnyName)*
	;

import_as_name
	:  AnyName ["as" Name]
	;

global_stmt
	:  <GLOBAL> Name ("," Name)*
	;

exec_stmt
	:  "exec" expr ["in" exec_stmt_end]
	;

exec_stmt_end
	:  test ["," test]
	;

assert_stmt
	:  "assert" test ["," test]
	;

if_stmt
	:  "if" test ":" if_stmt_End
	;

if_stmt_End
	:  small_stmt <NEWLINE> (elif_stmt)* [else_stmt]
	|  suite (elif_stmt)* [else_stmt]
	;

elif_stmt
	:  "elif" test ":" elif_stmt_End
	;

elif_stmt_End
	:  small_stmt <NEWLINE>
	|  suite
	;

else_stmt
	:  "else" ":" else_stmt_End
	;

else_stmt_End
	:  small_stmt <NEWLINE>
	|  suite
	;

while_stmt
	:  "while" test ":" while_stmt_End
	;

while_stmt_End
	:  small_stmt <NEWLINE>
	|  suite [else_stmt]
	;

for_stmt
	:  "for" exprlist "in" SmartTestList ":" for_stmt_End
	;

for_stmt_End
	:  small_stmt <NEWLINE>
	|  suite [else_stmt]
	;

try_stmt
	:  "try" ":" try_stmt_End (except_clause)* [finally_stmt]
	;

try_stmt_End
	:  small_stmt <NEWLINE>
	|  suite
	;

except_clause
	:  "except" [except_clause_test] ":" except_clause_End [else_stmt]
	;

except_clause_test
	:  test [except_clause_test_End]
	;

except_clause_test_End
	:  "," test
	|  "as" test
	;

except_clause_End
	:  small_stmt <NEWLINE>
	|  suite
	;

finally_stmt
	:  "finally" ":" finally_stmt_End
	;

finally_stmt_End
	:  small_stmt <NEWLINE>
	|  suite
	;

with_stmt
	:  "with" with_item ("," with_item)* ":" suite
	;

with_item
	:  test ("as" expr)?
	;

test
	:  lambdef
	|  and_test ("or" and_test)*
	;

and_test
	:  not_test ("and" not_test)*
	;

not_test
	:  "not" not_test
	|  comparison
	;

comparison
	:  expr (compEnd)*
	;

compEnd
	:  comp_op expr
	;

comp_op
	:  <LESS>
	|  <GREATER>
	|  <EQEQUAL>
	|  <EQGREATER>
	|  <EQLESS>
	|  <LESSGREATER>
	|  <NOTEQUAL>
	|  <IN>
	|  "not" <IN>
	|  LL(2) <IS> "not"
	|  <IS>
	;

expr
	:  xor_expr ("|" xor_expr)*
	;

xor_expr
	:  and_expr ("^" and_expr )*
	;

and_expr
	:  shift_expr ("&" shift_expr)*
	;

shift_expr
	:  arith_expr (shift_exprEnd )*
	;

shift_exprEnd
	:  "<<" arith_expr
	|  ">>" arith_expr
	;

arith_expr
	:  term (arith_exprEnd)*
	;

arith_exprEnd
	:  <PLUS> term
	| <MINUS> term
	;

term
	:  factor (termEnd)*
	;

termEnd
	:  "*" factor
	|  <DIVIDE> factor
	|  <FLOORDIVIDE> factor
	|  <MODULO> factor
	;

factor
	:  <PLUS> factor
	|  <MINUS> factor
	|  <NOT> factor
	|  powerfactor
	;

powerfactor
	:  atomtrailer (LL(2) "**" factor)*
	;

atomtrailer
	:  atom (atomtrailerEnd)*
	;

atomtrailerEnd
	:  LL(2) "(" ")" <NONE>
	|  "(" arglist ")"
	|  "[" subscriptlist "]"
	|  <DOT> AnyName
	;

atom
	:  LL(2) "(" ")" <NONE>
	|  LL(2) Name "=" test
	|  "(" [SmartTestList] ")"
	|  <LBRACKET> [listmaker] "]"
	|  "{" [dictmaker] "}"
	|  "`" SmartTestList "`"
	|  Name
	|  Number
	|  StringNode (StringNode)*
	;

lambdef
	:  <LAMBDA> [varargslist] ":" test
	;

subscriptlist
	:  subscript (LL(2) "," subscript)* [","<NONE>]
	;

subscript
	:  <DOT> <DOT> <DOT>
	|  test (slice)?
	|  slice
	;

slice
	:  ct [ct]
	;

ct
	:  ":" [test]
	;

exprlist
	:  expr (LL(2) "," expr)* [","<NONE>]
	;

@FSTExportName("{TOSTRING}")
SmartTestList
	:  test (LL(2) "," test)* [","<NONE>]
	;

testlist
	:  test (LL(2) "," test)* [","<NONE>]
	;

dictmaker
	:  test ":" test (LL(2) dictInt)* [","<NONE>]
	;

dictInt
	:  "," test ":" test
	;

listmaker
	:  test listmakerEnd
	;

listmakerEnd
	:  (list_for)+
	|  (LL(2) "," test)* [","<NONE>]
	;

list_for:
 	"for" exprlist "in" SmartTestList (list_if)* ;

list_if
	:  "if" test
	;

decorators
	:  (decorator @!)+
	;

decorator
	:  "@" dotted_name decorator_End
	;

decorator_End
	:  <NEWLINE>
	|  "(" decorator_End_Par
	;

decorator_End_Par
	:  LOOK_AHEAD(1) ")"<NONE> <NEWLINE>
	|  arglist ")"<NONE> <NEWLINE>
	;

@FSTExportName("{<NAME>}({parameters})")
funcdef
	:  [decorators] "def" <NAME> parameters ":" funcdef_End
	;

funcdef_End
	:  small_stmt <NEWLINE>
	|  suite
	;

@FSTNonTerminal(name="{ClassName}({testlist})")
classdef
	:  [decorators] "class" ClassName ["(" testlist ")"] ":" @! (classdef_End)+
	;

@FSTExportName("{<NAME>}")
ClassName
	:  <NAME>
	;

classdef_End
	:  @FSTTerminal(name="{small_stmt}", merge="SemanticConflict") small_stmt
	|  @FSTNonTerminal() classsuite
	;

@FSTInline
classsuite
	:  <NEWLINE> <INDENT> @+ (classsimple_stmt)+ @- <DEDENT>
	;

@FSTNonTerminal(name="{classsmall_stmt}")
classsimple_stmt
	:  classsmall_stmt (<NEWLINE>)? @!
	;

classsmall_stmt
	:  @FSTTerminal(name="{funcdef}", merge="LineBased") @+ LOOK_AHEAD("(decorators())? \"def\"") funcdef
	|  @FSTNonTerminal(name="{classdef}") classdef
	|  @FSTTerminal(name="{FieldDeclaration}", merge="SemanticConflict") @+ LOOK_AHEAD("Name() \"=\"") FieldDeclaration
	|  @FSTTerminal(name="{AUTO}") expr_stmt
	|  @FSTTerminal(name="{TOSTRING}",merge="SemanticConflict") try_stmt
	|  @FSTTerminal(name="{TOSTRING}",merge="SemanticConflict") print_stmt
	|  @FSTTerminal(name="{TOSTRING}",merge="SemanticConflict") del_stmt
	|  @FSTTerminal(name="{TOSTRING}",merge="SemanticConflict") pass_stmt
	|  @FSTTerminal(name="{TOSTRING}",merge="SemanticConflict") flow_stmt
	|  @FSTTerminal(name="{TOSTRING}",merge="SemanticConflict") import_stmt
	|  @FSTTerminal(name="{TOSTRING}",merge="SemanticConflict") global_stmt
	|  @FSTTerminal(name="{TOSTRING}",merge="SemanticConflict") exec_stmt
	|  @FSTTerminal(name="{TOSTRING}",merge="SemanticConflict") assert_stmt
	|  @FSTTerminal(name="{AUTO}", merge="SemanticConflict") for_stmt
	|  @FSTTerminal(name="{AUTO}", merge="SemanticConflict") if_stmt
	;

@FSTExportName("{Field}")
FieldDeclaration
	:  Field (";" Field)*
	;

@FSTExportName("{<NAME>}")
Field
	:  <NAME> "=" SmartTestList
	;

@FSTInline
suite
	:  <NEWLINE> <INDENT> @+ (stmt)+ @- <DEDENT>
	;

arglist
	:  normalargs [arglist1End]
	|  [LL(2) arglist1EndEnd]
	;

arglist1End
	:  ","  [LL(2) arglist1EndEnd]
	;

arglist1EndEnd
	:  LL(2) ExtraArgValueList ["," ExtraKeywordValueList]
	|  ExtraKeywordValueList
	;

normalargs
	:  argument (LL(2) "," argument)*
	;

ExtraArgValueList
	:  "*" test
	;

ExtraKeywordValueList
	:  power test
	;

power
	:  "**"<NONE>
	|  "*" "*" <NONE>
	;

argument
	:  [LL(2) AnyName "="] test
	;

Number
	:  <HEXNUMBER>
	|  <OCTNUMBER>
	|  <DECNUMBER>
	|  <FLOAT>
	|  <COMPLEX>
	;

Complex
	:  <FLOAT>
	;

Name
	:  <NAME>
	|  <AS>
	;

StringNode
	:  <SINGLE_STRING>
	|  <SINGLE_STRING2>
	|  <TRIPLE_STRING>
	|  <TRIPLE_STRING2>
	|  <SINGLE_USTRING>
	|  <SINGLE_USTRING2>
	|  <TRIPLE_USTRING>
	|  <TRIPLE_USTRING2>
	;

AnyName
	:  <NAME>
	|  "or"
	|  "and"
	|  "not"
	|  <IS>
	|  <IN>
	|  <LAMBDA>
	|  <IF>
	|  <ELSE>
	|  <ELIF>
	|  <WHILE>
	|  <FOR>
	|  <TRY>
	|  <EXCEPT>
	|  <DEF>
	|  <CLASS>
	|  <FINALLY>
	|  <PRINT>
	|  <PASS>
	|  <BREAK>
	|  <CONTINUE>
	|  <RETURN>
	|  <YIELD>
	|  <IMPORT>
	|  <FROM>
	|  <DEL>
	|  <RAISE>
	|  <GLOBAL>
	|  <EXEC>
	|  <ASSERT>
	|  <AS>
	;
